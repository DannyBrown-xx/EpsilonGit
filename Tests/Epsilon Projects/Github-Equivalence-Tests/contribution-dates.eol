"The following number of commits happened per day across the whole repository:".println();
printDatesToCommits(Commit.all);
"\n\n".print();

for(committer in Committer.all) {
	(committer.getName() + " (" + committer.getEmailAddress() + ") committed this many times per day:").println();
	printDatesToCommits(committer.getAllCommittedCommits());
	"\n\n".print();
}

operation printDatesToCommits(commitsList) {  
	var datesToNumberCommits : Map;
	for(commit in commitsList.sortBy(c | c.getCommitCalendarDate())) {
		var commitDate = commit.getCommitCalendarDate();
	
		if(datesToNumberCommits.containsKey(commitDate)) {
			datesToNumberCommits.put(commitDate, datesToNumberCommits.get(commitDate) + 1);
		}
		else {
			datesToNumberCommits.put(commitDate, 1);
		}
	}
	
	for(date in datesToNumberCommits.keySet().sortBy(k | k)) {
		(date.getDay() + "/" + date.getMonth() + "/" + date.getYear()).print();
		" - ".print();
		datesToNumberCommits.get(date).print();
		" commits".println();
	}
}